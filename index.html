<!DOCTYPE html>
<html lang="en">
	<head>
		<title>HOTSAR | 首页</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<link rel="stylesheet" type="text/css" href="./index.css"></link>
	</head>

	<body>
		<div class="wrapper">
			<canvas id="logo_show_canvas"></canvas>
		</div>


		<script id="posFShaderText" type="x-shader/x-vertex">
uniform float delta;
uniform float floorPosY;
uniform bool isReset;
uniform sampler2D textureOriginPosition;
uniform vec3 interactPosition;
void main()	{
	vec2 uv = gl_FragCoord.xy / resolution.xy;
	vec4 position = texture2D( texturePosition, uv );
	vec4 originPosition = texture2D( textureOriginPosition, uv );
	vec4 velocity = texture2D( textureVelocity, uv );
	float lifeValue = 100.0;
	if (distance(vec3(interactPosition.xy, position.z), position.xyz) < 12. && position.w == 0.) {
		position.w = lifeValue;
	}
	float active = 0.0;
	float newLife = position.w;
	if (position.w >= lifeValue / 2.0) {
		active = 1.0;
		newLife -= 1.0 * 0.3;
	} else {
		active = 0.0;
		position.xyz = originPosition.xyz;
		newLife -= 1.0 * 3.;
	}

	newLife = clamp(newLife, 0., lifeValue);
	
	gl_FragColor = vec4( position.xyz + velocity.xyz * active , newLife );
}
		</script>
		<script id="velFShaderText" type="x-shader/x-fragment">

uniform float delta;
uniform float floorPosY;
uniform vec3 forcePos;
uniform float forceRadius;
uniform bool isReset;
uniform float timeScale;
uniform float life;
uniform vec3 evolution;
vec3 g = vec3(0, -8, 0);
//
// Description : Array and textureless GLSL 2D/3D/4D simplex
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//
vec3 mod289(vec3 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 mod289(vec4 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 permute(vec4 x) {
	return mod289(((x*34.0)+1.0)*x);
}
vec4 taylorInvSqrt(vec4 r){
	return 1.79284291400159 - 0.85373472095314 * r;
}
float snoise(vec3 v) {
	const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
	const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
	// First corner
	vec3 i  = floor(v + dot(v, C.yyy) );
	vec3 x0 =   v - i + dot(i, C.xxx) ;
	// Other corners
	vec3 g = step(x0.yzx, x0.xyz);
	vec3 l = 1.0 - g;
	vec3 i1 = min( g.xyz, l.zxy );
	vec3 i2 = max( g.xyz, l.zxy );
	//   x0 = x0 - 0.0 + 0.0 * C.xxx;
	//   x1 = x0 - i1  + 1.0 * C.xxx;
	//   x2 = x0 - i2  + 2.0 * C.xxx;
	//   x3 = x0 - 1.0 + 3.0 * C.xxx;
	vec3 x1 = x0 - i1 + C.xxx;
	vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
	vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y
	// Permutations
	i = mod289(i);
	vec4 p = permute( permute( permute(
		  i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
		+ i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
		+ i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
	// Gradients: 7x7 points over a square, mapped onto an octahedron.
	// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
	float n_ = 0.142857142857; // 1.0/7.0
	vec3  ns = n_ * D.wyz - D.xzx;
	vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)
	vec4 x_ = floor(j * ns.z);
	vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)
	vec4 x = x_ *ns.x + ns.yyyy;
	vec4 y = y_ *ns.x + ns.yyyy;
	vec4 h = 1.0 - abs(x) - abs(y);
	vec4 b0 = vec4( x.xy, y.xy );
	vec4 b1 = vec4( x.zw, y.zw );
	//vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
	//vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
	vec4 s0 = floor(b0)*2.0 + 1.0;
	vec4 s1 = floor(b1)*2.0 + 1.0;
	vec4 sh = -step(h, vec4(0.0));
	vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
	vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
	vec3 p0 = vec3(a0.xy,h.x);
	vec3 p1 = vec3(a0.zw,h.y);
	vec3 p2 = vec3(a1.xy,h.z);
	vec3 p3 = vec3(a1.zw,h.w);
	//Normalise gradients
	vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;
	// Mix final noise value
	vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
	m = m * m;
	return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3) ) );
}
vec3 snoiseVec3( vec3 x ){
	float s  = snoise(vec3( x ));
	float s1 = snoise(vec3( x.y - 19.1 , x.z + 33.4 , x.x + 47.2 ));
	float s2 = snoise(vec3( x.z + 74.2 , x.x - 124.5 , x.y + 99.4 ));
	vec3 c = vec3( s , s1 , s2 );
	return c;
}
vec3 curlNoise( vec3 p ){
	const float e = .1;
	vec3 dx = vec3( e   , 0.0 , 0.0 );
	vec3 dy = vec3( 0.0 , e   , 0.0 );
	vec3 dz = vec3( 0.0 , 0.0 , e   );
	vec3 p_x0 = snoiseVec3( p - dx );
	vec3 p_x1 = snoiseVec3( p + dx );
	vec3 p_y0 = snoiseVec3( p - dy );
	vec3 p_y1 = snoiseVec3( p + dy );
	vec3 p_z0 = snoiseVec3( p - dz );
	vec3 p_z1 = snoiseVec3( p + dz );
	float x = p_y1.z - p_y0.z - p_z1.y + p_z0.y;
	float y = p_z1.x - p_z0.x - p_x1.z + p_x0.z;
	float z = p_x1.y - p_x0.y - p_y1.x + p_y0.x;
	const float divisor = 1.0 / ( 2.0 * e );
	return normalize( vec3( x , y , z ) * divisor );
}
vec3 curlNoise2( vec3 p ) {
	const float e = .1;
	vec3 xNoisePotentialDerivatives = snoiseVec3( p );
	vec3 yNoisePotentialDerivatives = snoiseVec3( p + e * vec3( 3., -3.,  1. ) );
	vec3 zNoisePotentialDerivatives = snoiseVec3( p + e * vec3( 2.,  4., -3. ) );
	vec3 noiseVelocity = vec3(
		zNoisePotentialDerivatives.y - yNoisePotentialDerivatives.z,
		xNoisePotentialDerivatives.z - zNoisePotentialDerivatives.x,
		yNoisePotentialDerivatives.x - xNoisePotentialDerivatives.y
	);
	return normalize( noiseVelocity );
}
vec4 snoiseD(vec3 v) { //returns vec4(value, dx, dy, dz)
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy;
  vec3 x3 = x0 - D.yyy;
  i = mod289(i);
  vec4 p = permute( permute( permute(
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;
  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);
  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );
  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);
  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));
  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;
  vec4 values = vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3) ); //value of contributions from each corner (extrapolate the gradient)
  vec4 m = max(0.5 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0); //kernel function from each corner
  vec4 m2 = m * m;
  vec4 m3 = m * m * m;
  vec4 temp = -6.0 * m2 * values;
  float dx = temp[0] * x0.x + temp[1] * x1.x + temp[2] * x2.x + temp[3] * x3.x + m3[0] * p0.x + m3[1] * p1.x + m3[2] * p2.x + m3[3] * p3.x;
  float dy = temp[0] * x0.y + temp[1] * x1.y + temp[2] * x2.y + temp[3] * x3.y + m3[0] * p0.y + m3[1] * p1.y + m3[2] * p2.y + m3[3] * p3.y;
  float dz = temp[0] * x0.z + temp[1] * x1.z + temp[2] * x2.z + temp[3] * x3.z + m3[0] * p0.z + m3[1] * p1.z + m3[2] * p2.z + m3[3] * p3.z;
  return vec4(dot(m3, values), dx, dy, dz) * 42.0;
}
vec3 curlNoise3 (vec3 p) {
    vec3 xNoisePotentialDerivatives = snoiseD( p ).yzw; //yzw are the xyz derivatives
    vec3 yNoisePotentialDerivatives = snoiseD(vec3( p.y - 19.1 , p.z + 33.4 , p.x + 47.2 )).zwy;
    vec3 zNoisePotentialDerivatives = snoiseD(vec3( p.z + 74.2 , p.x - 124.5 , p.y + 99.4 )).wyz;
    vec3 noiseVelocity = vec3(
        zNoisePotentialDerivatives.y - yNoisePotentialDerivatives.z,
        xNoisePotentialDerivatives.z - zNoisePotentialDerivatives.x,
        yNoisePotentialDerivatives.x - xNoisePotentialDerivatives.y
    );
	const float e = .1;
	const float divisor = 1.0 / ( 2.0 * e );
	return normalize( noiseVelocity * divisor );
}
void main() {
	float velFactor = 1./(300.*timeScale);
	vec2 uv = gl_FragCoord.xy / resolution.xy;
	vec4 position = texture2D( texturePosition, uv );
	vec4 velocity = texture2D( textureVelocity, uv );
	vec3 v = 0.3 * ( curlNoise( .05 * position.xyz + evolution) );
	gl_FragColor = vec4( v, velocity.w);
}
		</script>
		<script id="vertexShaderText" type="x-shader/x-vertex">
precision highp float;
attribute vec3 position;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 normalMatrix;
uniform mat4 modelMatrix;
uniform mat4 viewMatrix;
uniform vec3 cameraPosition;
uniform sampler2D texturePosition;
uniform sampler2D textureVelocity;
uniform vec3 boxVertices[ 36 ];
uniform vec3 boxNormals[ 3 ];
uniform float width;
uniform float height;
uniform float timer;
uniform vec3 boxScale;
uniform float meshScale;
uniform mat4 shadowV;
uniform mat4 shadowP;
uniform vec3 lightPosition;

varying vec3 vPosition;
varying vec4 vColor;
varying vec4 vShadowCoord;
varying vec3 vNormal;
varying vec3 vLightPosition;

const mat4 biasMatrix = mat4(
	0.5, 0.0, 0.0, 0.0,
	0.0, 0.5, 0.0, 0.0,
	0.0, 0.0, 0.5, 0.0,
	0.5, 0.5, 0.5, 1.0
);
mat4 rotationMatrix(vec3 axis, float angle) {
	axis = normalize(axis);
	float s = sin(angle);
	float c = cos(angle);
	float oc = 1.0 - c;
	return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
				oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
				oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
				0.0,                                0.0,                                0.0,                                1.0);
}
float ramp( float x ) {
	return 1. - 1. - pow( 1. - x, 4. );
}
float parabola( float x, float k ) {
	return pow( 4. *(x - 0.5)*(x - 0.5), k );
}
float random(vec4 seed4){
	float dot_product = dot(seed4, vec4(12.9898,78.233,45.164,94.673));
	return fract(sin(dot_product) * 43758.5453);
}
mat3 calcLookAtMatrix(vec3 origin, vec3 target, float roll) {
	vec3 rr = vec3(sin(roll), cos(roll), 0.0);
	vec3 ww = normalize(target - origin);
	vec3 uu = normalize(cross(ww, rr));
	vec3 vv = normalize(cross(uu, ww));
	return mat3(uu, vv, ww);
}
void main() {
	vec2 dimensions = vec2( width, height );
	float px = position.y;
	float vi = position.z;
	float x = mod( px, dimensions.x );
	float y = mod( floor( px / dimensions.x ), dimensions.y );
	vec2 uv = vec2( x, y ) / dimensions;
	vec4 cubePosition = texture2D( texturePosition, uv );
	vec4 prevPosition = cubePosition - texture2D( textureVelocity, uv );
	float lifeValue = 100.0;
	float alpha = clamp(cubePosition.w, 0., lifeValue) / lifeValue;
	float scale = parabola(alpha, 0.3);

	vec3 faceNormal = boxNormals[ int( vi / 6. ) ];

	mat4 localRotation = mat4( calcLookAtMatrix( cubePosition.xyz, prevPosition.xyz, 0. ) );
	vec4 rotatedNormal = localRotation * vec4( faceNormal, 1. );
	vec3 visPosition = ( modelMatrix * ( cubePosition + rotatedNormal * scale ) ).xyz;
	float d = dot( normalize( visPosition - cameraPosition ), normalize( ( modelMatrix * rotatedNormal ).xyz ) );
	vec3 boxVertex = boxVertices[ int( vi + ( 1. - step( 0., d ) ) * 18. ) ];
	vec3 modifiedVertex = ( ( localRotation * vec4( boxVertex * scale * boxScale * meshScale, 1. ) ).xyz );
	vec3 modifiedPosition = cubePosition.xyz + modifiedVertex;

	gl_Position = projectionMatrix * modelViewMatrix * vec4( modifiedPosition, 1.0 );


	vShadowCoord = biasMatrix * shadowP * shadowV * modelMatrix * vec4( modifiedPosition, 1. );
	vPosition = modifiedPosition;
	vNormal = rotatedNormal.xyz;
	vColor = vec4(0.4, 0.4, 0.4, 1.0);
	vLightPosition = lightPosition;
}
		</script>

		<script id="fragmentShaderText" type="x-shader/x-fragment">

#extension GL_OES_standard_derivatives : enable
precision highp float;

uniform vec2 resolution;
uniform vec3 cameraPosition;
uniform vec2 windowSize;
uniform sampler2D overlayTexture;
uniform sampler2D depthTexture;

varying vec3 vPosition;
varying vec4 vColor;
varying vec4 vShadowCoord;
varying vec3 vLightPosition;

float bias;
float unpackDepth( const in vec4 rgba_depth ) {
	const vec4 bit_shift = vec4(1.0/(256.0*256.0*256.0), 1.0/(256.0*256.0), 1.0/256.0, 1.0);
    return dot(rgba_depth, bit_shift);
}
float random(vec4 seed4){
    float dot_product = dot(seed4, vec4(12.9898,78.233,45.164,94.673));
	return fract(sin(dot_product) * 43758.5453);
}
float sampleVisibility( vec3 coord ) {
	return step( coord.z, unpackDepth( texture2D( depthTexture, coord.xy + 0. * ( .5 - random( vec4( coord, bias ) ) ) / 2048. ) ) + bias );
}
mat2 rotationMatrix( float a ) {
	return mat2( cos( a ), sin( a ),
 			    -sin( a ), cos( a ) );
}
const float PI = 3.14159265358979323846264;
void main() {
	vec3 fdx = dFdx( vPosition );
	vec3 fdy = dFdy( vPosition );
	vec3 n = normalize(cross(fdx, fdy));
	vec4 base = vec4( 1. );
	vec3 L = normalize( vLightPosition - vPosition );
	vec3 E = normalize( cameraPosition - vPosition );
	float diffuse = max( 0., dot( L, n ) );
	float theta = clamp( -diffuse, 0., 1. );
    bias = 0.005 * tan( acos( theta ) );
    bias = clamp( bias, 0., 0.01 );

	float shadow = 0.;
	vec3 shadowCoord = vShadowCoord.xyz / vShadowCoord.w;
	float step = 4.;
	vec2 inc = vec2( step ) / resolution;
	shadow += sampleVisibility( shadowCoord + vec3(     0., -inc.y, 0. ) );
	shadow += sampleVisibility( shadowCoord + vec3( -inc.x,     0., 0. ) );
	shadow += sampleVisibility( shadowCoord + vec3(     0.,     0., 0. ) );
	shadow += sampleVisibility( shadowCoord + vec3(  inc.x,     0., 0. ) );
	shadow += sampleVisibility( shadowCoord + vec3(     0.,  inc.y, 0. ) );
	shadow /= 5.;
	//vec4 mask = texture2D( projector, vShadowCoord.xy );

	float shininess = 10.;
	vec3 halfVector = normalize(E + L );
	float specular = dot(n, halfVector);
	specular = max(0.0, specular);
	specular = pow(specular, shininess);
	float ambient = .2;
	float o = diffuse * shadow;
	vec3 color = mix( vColor.rgb, vec3( 1. ), .8 * clamp( -n.y, 0., 1. ) );
	vec3 diffuseColor = color * mix( vec3( o ), vec3( 1. ), ambient );
	vec3 specularColor = vec3( 1. );
	base.rgb = mix( diffuseColor, specularColor, specular * o );

	// Overlay
	vec2 screenUv = (gl_FragCoord.xy - vec2(0.5, 0.5)) / windowSize.xx;
	screenUv = mod(screenUv, vec2(1., 1.));
	vec3 overlayColor = texture2D(overlayTexture, screenUv).xyz ;

	gl_FragColor = base/* * vec4(overlayColor, 1.0)*/;
}
		</script>

		<script id="fs-particles-shadow" type="x-shader/x-fragment">

precision highp float;

vec4 packDepth(const in float depth) {
	const vec4 bit_shift = vec4(256.0*256.0*256.0, 256.0*256.0, 256.0, 1.0);
	const vec4 bit_mask  = vec4(0.0, 1.0/256.0, 1.0/256.0, 1.0/256.0);
	vec4 res = mod(depth*bit_shift*vec4(255), vec4(256))/vec4(255);
	res -= res.xxyz * bit_mask;
	return res;
}

void main() {

	gl_FragColor = packDepth( gl_FragCoord.z );

}

		</script>
		<script src="js/jquery-3.2.1.min.js"></script>
		<script src="js/Tween.js"></script>
		<script src="js/three.js"></script>
		<script src="js/OrbitControls.js?12"></script>
		<script src="js/GPUComputationRenderer.js?1"></script>
		<script src="index.js?212"></script>

	</body>
</html>
